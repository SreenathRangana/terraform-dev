##### ACM Certificate Request with DNS Validation #####
resource "aws_acm_certificate" "cert" {
  domain_name       = var.domain_name
  validation_method = "DNS"

  lifecycle {
    create_before_destroy = true
  }

  tags = {
    Name = "${var.env_name}-${var.project_name}-acm"
  }
}

##### Route 53 Hosted Zone #####
resource "aws_route53_zone" "main" {
  name = var.domain_name

  tags = {
    Name = "${var.env_name}-${var.project_name}-r53"
  }
}

##### ACM DNS Validation Records #####
resource "aws_route53_record" "cert_validation" {
  for_each = {
    for dvo in aws_acm_certificate.cert.domain_validation_options : dvo.domain_name => {
      name   = dvo.resource_record_name
      type   = dvo.resource_record_type
      record = dvo.resource_record_value
    }
  }

  zone_id = aws_route53_zone.main.zone_id
  name    = each.value.name
  type    = each.value.type
  records = [each.value.record]
  ttl     = 60
}

##### ACM Certificate Validation #####
resource "aws_acm_certificate_validation" "cert" {
  certificate_arn         = aws_acm_certificate.cert.arn
  validation_record_fqdns = [for record in aws_route53_record.cert_validation : record.fqdn]
}
